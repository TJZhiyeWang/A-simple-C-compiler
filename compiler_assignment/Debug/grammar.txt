Program ::= <声明串>
<声明串> ::= <声明> <声明串sub>
<声明串sub> ::= <声明> <声明串sub> | Empty
<声明> ::= int ID <声明类型> | void ID <函数声明>
<声明类型> ::= <变量声明> | <函数声明>
<变量声明> ::= ;
<函数声明> ::= '(' <形参> ')' <语句块>
<形参> ::= <参数列表> | void
<参数列表> ::= <参数> <参数列表sub>
<参数列表sub> ::= , <参数> <参数列表sub> | Empty
<参数> ::= int ID
<语句块> ::= '{' <内部声明> <语句串> '}'
<内部声明> ::= Empty | <内部变量声明> <内部声明>
<内部变量声明> ::= int ID ;
<语句串> ::= <语句> <语句串> | Empty
<语句> ::= <if语句> | <while语句> | <return语句> | <赋值语句>
<赋值语句> ::= ID = <表达式> ;
<return语句> ::= return <return语句sub> ;
<return语句sub> ::= <表达式> | Empty
<while语句> ::= while '(' <表达式> ')' <语句块>
<if语句> ::= if '(' <表达式> ')' <语句块> <if语句sub>
<if语句sub> ::= else <语句块> | Empty
<表达式> ::= <加法表达式> <表达式sub>
<表达式sub> ::= relop <加法表达式> <表达式sub> | Empty
relop ::= < | <= | > | >= | == | !=
<加法表达式> ::= <项> <加法表达式sub>
<加法表达式sub> ::= + <项> <加法表达式sub> | - <项> <加法表达式sub> | Empty
<项> ::= <因子> <项sub>
<项sub> ::= * <因子> <项sub> | / <因子> <项sub> | Empty
<因子> ::= NUM | '(' <表达式> ')' | ID FTYPE
FTYPE ::= <call> | Empty
<call> ::= '(' <实参列表> ')'
<实参> ::= <实参列表> | Empty
<实参列表> ::= <表达式> <实参列表sub>
<实参列表sub> ::= , <表达式> <实参列表sub> | Empty
